import com.rameses.annotations.*;
import java.rmi.server.*;
import com.rameses.util.*;
import com.rameses.functions.*;
import java.text.*;

class WaterworksMobileSettingService {

	@Service("RuleMgmtService")
	def ruleMgmt;

	@DataContext("sys_rule")
	def ruleEm;


	def buildRuleText( def rule ) {
		def vars = [];
		def conditions = [];
		def actions = [];
		rule.conditions.each {
			def factName = "facts." + it.fact.factclass.substring( it.fact.factclass.lastIndexOf(".") + 1 );
			if( it.varname ) vars << [name: it.varname, value: factName];
			it.constraints.findAll{ it.varname !=null }.each { ct ->
				vars << [name: ct.varname, value: factName +"." + ct.field.name];
			}
			def conds = it.constraints.findAll{ it.operator?.symbol !=null };
			conds.each { x->
				def val = null;
				if(x.decimalvalue) val = x.decimalvalue;
				else if(x.intvalue) val = x.intvalue;
				else if(x.stringvalue) val = "'" + x.stringvalue + "'";
				else if(x.listvalue) {
					val = "'.*" + x.listvalue.key.join("|") + ".*'";
				}	
				def vv = [];
				vv << factName + "." + x.field.name;
				if( x.operator.symbol == "matches" ) {
					vv << ".match(";
					vv << val;
					vv << ")!=null";
				}	
				else {
					vv << x.operator.symbol;
					if(val!=null) vv << val;
				}
				conditions << vv; 
			}
		}
		rule.actions.each {
			def act = [ name:  it.actiondef.objid ]
			if( it.actiondef.objid.toLowerCase().matches(".*computeconsumption")  ) {
				def amtParam = it.params.find{ pt-> pt.actiondefparam.name=='amount' };
				def expr =amtParam.expr.replace("def", "var");
				def refParam = it.params.find{ pt-> pt.actiondefparam.name=='ref' };

				def actionName = "action"+it.pos;
				def sbuff = new StringBuffer();
				sbuff.append( "var " + actionName + " =function() {");
				sbuff.append( expr );
				sbuff.append("};");
				sbuff.append( refParam.var.name +".amount="+actionName+ "();" );
				act.expr = sbuff.toString();
			}
			actions << act;
		}

		def buff = new StringBuilder();
		buff.append( "function " + rule.name +  "(facts,result) { ");
		vars.each {
			buff.append( "var " + it.name + "=" + it.value +";" );
		}
		if(conditions) {
			buff.append("if( ")
			for( int i=0; i < conditions.size(); i++) {
				if(i !=0 ) buff.append( " && ");
				buff.append( conditions[i].join("") );
			}
			buff.append( ") { ");
			actions.each { a->
				buff.append( a.expr );
			}
			buff.append( "}");
		}
		buff.append( "}");
		return buff.toString();
	}

	@ProxyMethod
	public def getComputationFeeRules() {
		def list = ruleEm.find( [ruleset: "waterworkscomputation", rulegroup: "initial", state: "deployed"]).orderBy("salience DESC").list();

		def result = [];
		for( o in list ) {
			def r = ruleMgmt.open( [objid: o.objid] );
			def m = buildRuleText(r);
			result << m;
		}

		return result;
	}

}